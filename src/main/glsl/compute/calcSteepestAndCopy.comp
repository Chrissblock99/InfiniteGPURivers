#version 450 core
layout(local_size_x = 1, local_size_y = 1) in;

layout(binding = 0, r32f) readonly uniform image2D heightMap;
layout(binding = 1, r32f) readonly uniform image2D drainageAreaMap;

layout(binding = 2, r8i) writeonly uniform iimage2D steepestNeighbourOffsetIndexMap;
layout(binding = 3, r32f) writeonly uniform image2D receiverHeightMap;
layout(binding = 4, r32f) writeonly uniform image2D drainageAreaCopyMap;
layout(binding = 5, r32f) writeonly uniform image2D laplacianMap;



uniform ivec2 srcPos;



const float cellSizeInverse = 1.0/500.0;

const ivec2 mooreNeighbourhood[9] = ivec2[](
    ivec2( 0,  0),
    ivec2(-1,  1),
    ivec2( 0,  1),
    ivec2( 1,  1),
    ivec2(-1,  0),
    ivec2( 1,  0),
    ivec2(-1, -1),
    ivec2( 0, -1),
    ivec2( 1, -1)
);

const float inverseMooreNeighbourhoodDistances[9] = float[](
    0,
    1/sqrt(2),
    1,
    1/sqrt(2),
    1,
    1,
    1/sqrt(2),
    1,
    1/sqrt(2)
);

float slope(float height, float otherHeight, int offsetIndex) {
    return (otherHeight - height) * inverseMooreNeighbourhoodDistances[offsetIndex] * cellSizeInverse;
}

int steepest(float heights[9]) {
    float height = heights[0];
    int index = 0;
    float minSlope = 0.0f;
    for (int i = 1; i < 9; i++) {
        float currentSlope = slope(height, heights[i], i);
        if (currentSlope < minSlope) {
            minSlope = currentSlope;
            index = i;
        }
    }
    return index;
}

float laplacian(float heights[9]) {
    float lapl = 0.0f;

    lapl += heights[5] - 2.0f * heights[0] + heights[4];

    lapl += heights[2] - 2.0f * heights[0] + heights[7];

    return lapl * cellSizeInverse * cellSizeInverse;
}

void main() {
    ivec2 pos = srcPos + ivec2(gl_GlobalInvocationID);

    float heights[9];
    for (int i = 0; i < 9; i++)
        heights[i] = imageLoad(heightMap, pos + mooreNeighbourhood[i]).x;

    int index = steepest(heights);
    imageStore(steepestNeighbourOffsetIndexMap, pos, ivec4(index));
    imageStore(receiverHeightMap, pos, vec4(heights[index]));
    imageStore(drainageAreaCopyMap, pos, imageLoad(drainageAreaMap, pos));
    imageStore(laplacianMap, pos, vec4(laplacian(heights)));
}